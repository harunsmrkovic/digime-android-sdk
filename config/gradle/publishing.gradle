/*
 * Copyright (c) 2009-2018 digi.me Limited. All rights reserved.
 */

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def repositoryUrl = "https://repository.sysdigi.net/m2/"
def repositoryKey = getRepositoryKey()
def repositoryUsername = getPropertyFromFile("REPO_USERNAME", '../config/props/pub.properties')
def repositoryPassword = getPropertyFromFile("REPO_PASSWORD", '../config/props/pub.properties')

//TODO choose repository key in artifact's module which we're going to deploy
def getRepositoryKey() {
    if ("api.digi.me" == project.ext.exports.globalEndpoint) {
        return "libs-release-local"
    } else {
        return "libs-snapshot-local"
    }
}

def getPropertyFromFile(prop, filepath) {
    def file = project.file(filepath)
    if (file.canRead()) {
        def properties = new Properties()
        properties.load(file.newDataInputStream())
        return properties.getProperty(prop)
    }
    return ""
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'me.digi.sdk'
            version = project.version
            artifactId project.getName()

            artifact("$buildDir/outputs/aar/${project.getName()}-${project.ext.globalVersion}.aar")

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.getByName("releaseCompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', it.moduleVersion)
                }
            }
        }
    }
}

artifactory {
    contextUrl = repositoryUrl
    publish {
        repository {
            repoKey = repositoryKey
            username = repositoryUsername
            password = repositoryPassword
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'dev.team': 'core']
            publishPom = true
        }
    }
}
